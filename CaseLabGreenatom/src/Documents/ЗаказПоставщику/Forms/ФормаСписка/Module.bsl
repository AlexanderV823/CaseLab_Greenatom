#Область ОбработчикиСобытийФормы


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСтатусы(Команда)
	
	ОчиститьСообщения();
	
	АктуализацияСтатусовЗаказовВызовСервера.АктуализироватьСтатусы();
	
	Элементы.Список.Обновить();
						
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаказПоОстаткам(Команда)
		
	НовыйЗаказ = СоздатьЗаказПоОстаткамНаСервере();
	
	ПараметрыФормы = Новый Структура("Ключ", НовыйЗаказ);
    ОткрытьФорму("Документ.ЗаказПоставщику.Форма.ФормаДокумента", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СоздатьЗаказПоОстаткамНаСервере()
	
	НовыйЗаказ = Документы.ЗаказПоставщику.СоздатьДокумент();
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РегистрСтраховойЗапас.Номенклатура КАК Номенклатура,
		|	ЦеныСрезПоследних.ЦенаБезНДС КАК ЦенаБезНДС,
		|	ЦеныСрезПоследних.СтавкаНДС КАК СтавкаНДС,
		|	ЦеныСрезПоследних.ЦенаСНДС КАК ЦенаСНДС,
		|	РегистрСтраховойЗапас.СрокПоставки КАК СрокПоставки,
		|	РегистрСтраховойЗапас.Номенклатура.ОбъемУпаковки КАК НоменклатураОбъемУпаковки
		|ИЗ
		|	РегистрСведений.СтраховойЗапас КАК РегистрСтраховойЗапас
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваров.Остатки(&Дата, ) КАК ОстаткиТоваровОстатки
		|		ПО РегистрСтраховойЗапас.Номенклатура = ОстаткиТоваровОстатки.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(&Дата, ) КАК ЦеныСрезПоследних
		|		ПО РегистрСтраховойЗапас.Номенклатура = ЦеныСрезПоследних.Номенклатура
		|ГДЕ
		|	ОстаткиТоваровОстатки.КоличествоОстаток < РегистрСтраховойЗапас.СтраховойЗапас";
	
	Запрос.УстановитьПараметр("Дата", КонецДня(ТекущаяДата()));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	МаксимальнаяДатаПоставки = НачалоДня(ТекущаяДата());
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НоваяСтрокаТЧ = НовыйЗаказ.Позиции.Добавить();
		НоваяСтрокаТЧ.Наименование = ВыборкаДетальныеЗаписи.Номенклатура;
		НоваяСтрокаТЧ.Количество = ВыборкаДетальныеЗаписи.НоменклатураОбъемУпаковки;
		НоваяСтрокаТЧ.СтавкаНДС = ВыборкаДетальныеЗаписи.СтавкаНДС;
		НоваяСтрокаТЧ.Цена = ВыборкаДетальныеЗаписи.ЦенаБезНДС;
		НоваяСтрокаТЧ.Сумма = ВыборкаДетальныеЗаписи.ЦенаБезНДС * ВыборкаДетальныеЗаписи.НоменклатураОбъемУпаковки;
		НоваяСтрокаТЧ.СуммаНДС = НДССервер.СуммаНДСПоСтавке(НоваяСтрокаТЧ.Сумма, НоваяСтрокаТЧ.СтавкаНДС); 
		НоваяСтрокаТЧ.СуммаСНДС = НоваяСтрокаТЧ.Сумма + НоваяСтрокаТЧ.СуммаНДС;
		
		//Вычислим самую крайнюю дату поставки из всех позиций с учетом сегоднящнего дня:
		ДатаПоставкиВыбраннойПозиции = НачалоДня(ТекущаяДата()) + (ВыборкаДетальныеЗаписи.СрокПоставки * 86400); 
		
		Если МаксимальнаяДатаПоставки < ДатаПоставкиВыбраннойПозиции Тогда
			
			МаксимальнаяДатаПоставки = ДатаПоставкиВыбраннойПозиции;
				
		КонецЕсли;
			
	КонецЦикла;
	
	НовыйЗаказ.Адрес = "г.Москва, ул.Б.Ордынка, д. 26";
	НовыйЗаказ.Дата = ТекущаяДата();
	НовыйЗаказ.ДатаПоставки = МаксимальнаяДатаПоставки;
	НовыйЗаказ.Комментарий = "Документ создан автоматической обработкой.";
	НовыйЗаказ.Записать();
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("Создан новый %1. Проверьте количество заказываемых позиций", НовыйЗаказ);
	Сообщение.КлючДанных = НовыйЗаказ.Ссылка;
   	Сообщение.Сообщить(); 
	
	Возврат НовыйЗаказ.Ссылка;
	
КонецФункции

#КонецОбласти